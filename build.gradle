import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'checkstyle'
}

group = 'xyz.soda'
version = '0.0.1-SNAPSHOT'
description = 'Skydive Dash'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
checkstyle {
    toolVersion = "10.12.7"
    configFile = file("config/checkstyle.xml")
    ignoreFailures = false
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'org.postgresql:postgresql'


    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'net.logstash.logback:logstash-logback-encoder:9.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    useJUnitPlatform()
}
spotless {
    java {
        target 'src/main/java/**/*.java', 'src/test/java/**/*.java'

        palantirJavaFormat()
        removeUnusedImports()
        endWithNewline()

    }
}
tasks.register('bootRunDev', BootRun) {
    group = 'application'
    description = 'Run the application with the dev Spring profile active'
    systemProperty 'spring.profiles.active', 'dev'
    // propagate JVM args if needed
    jvmArgs = System.getProperty('jvmArgs') ? System.getProperty('jvmArgs').split(' ') : []
    // ensure the runtime classpath (classes + dependencies) is used
    classpath = sourceSets.main.runtimeClasspath
    // specify the application's main class so BootRun can start the app
    mainClass.set('xyz.soda.slowfall.SlowfallApplication')
}
tasks.register('printHash', JavaExec) {
    group = 'tools'
    description = 'Print a BCrypt hash for the given password (use -Ppw=yourPassword)'
    classpath = sourceSets.main.runtimeClasspath
    main = 'xyz.soda.slowfall.tools.HashGen'
    if (project.hasProperty('pw')) {
        args project.property('pw')
    }
}
